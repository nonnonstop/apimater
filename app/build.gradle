plugins {
    id 'com.android.application'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    signingConfigs {
        release {
            storeFile file('./misc/keystore.jks')
            storePassword 'i1KY2P37COLmzLtE'
            keyAlias 'key0'
            keyPassword 'i1KY2P37COLmzLtE'
        }
    }
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.nonnonstop.apimate"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 500
        versionName "0.5.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        dataBinding true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation "androidx.activity:activity-ktx:1.2.3"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.fragment:fragment-ktx:1.3.4"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.work:work-runtime-ktx:2.5.0"
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'
    implementation 'org.mozilla:rhino:1.7.13'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

// Build TypeScript
import org.apache.tools.ant.taskdefs.condition.Os

class NodeExec extends Exec {
    NodeExec() {
        workingDir 'src/main/script'
        if (project.hasProperty('NPM_DIR')) {
            environment 'PATH', project['NPM_DIR']
        }
    }

    void npmCommandLine(String... args) {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            commandLine('cmd', '/c', 'npm.cmd', *args)
        } else {
            commandLine('npm', *args)
        }
    }
}

def prepareBuildAssetScripts = tasks.register('prepareBuildAssetScripts', NodeExec) {
    npmCommandLine 'install'
}

def buildAssetScripts = tasks.register('buildAssetScripts', NodeExec) {
    npmCommandLine 'run', 'build'
    dependsOn prepareBuildAssetScripts
}

def cleanBuildAssetScripts = tasks.register('cleanBuildAssetScripts', NodeExec) {
    npmCommandLine 'run', 'clean'
}

android.applicationVariants.all { variant ->
    variant.getMergeAssetsProvider().get().dependsOn(buildAssetScripts)
}

tasks.clean.dependsOn(cleanBuildAssetScripts)